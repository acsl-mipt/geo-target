"Модуль управления выдачей ТМ-информации"
namespace ru.mipt.acsl.tm

import ru.mipt.acsl.foundation.u8

'Циклический массив'
type ring_buf struct
(
  'Данные циклического массива'
  [u8] data,
  'Смещение начала цкилического массива'
  ber startOffset,
  'Смещение конца циклического массива'
  ber endOffset,
)

'Номер компонента и номер сообщения'
type comp_msg struct
(
  'Номер компонента'
  ber componentNum,
  'Номер сообщения в компоненте'
  ber messageNum,
)

'Информация о событии'
type event_info struct
(
  comp_msg componentMessage,
  'Номер события'
  ber      eventNum,
)

type tm_cmd_error enum ber (OK = 0, INVALID_ARGUMENT = 1, INVALID_STATE = 2, INVALID_MESSAGE_NUM = 3)

'Управление формированием и выдачей ТМ-информации'
component Tm
{
  parameters
  (
    'число разрешенных для выдачи сообщений'
    ber allowedMessages,
  )

  'Выдать статусное сообщение во внеочередную выдачу'
  command sendStatusMessage(comp_msg componentMessage) -> tm_cmd_error

  'Установить запрос на выдачу в соответствии с приоритетом'
  command setMessageRequest(comp_msg componentMessage, 'Приоритет' ber ^priority) -> tm_cmd_error

  'Снять запрос на выдачу в соответствии с приоритетом'
  command clearMessageRequest(comp_msg componentMessage) -> tm_cmd_error

  'Запретить формирование ТМ-сообщения'
  command denyMessage(comp_msg componentMessage) -> tm_cmd_error

  'Снять запрет на формирование ТМ-сообщения'
  command allowMessage(comp_msg componentMessage) -> tm_cmd_error

  'Установить запрет на формирование и выдачу события'
  command denyEvent(event_info eventInfo) -> tm_cmd_error

  'Снять запрет на формирование и выдачу события'
  command allowEvent(event_info eventInfo) -> tm_cmd_error
}